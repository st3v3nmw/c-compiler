OUTER_STMTS ::= TSPEC T_IDENTIFIER OUTER_STMT OUTER_STMTS
OUTER_STMTS ::= Ɛ

OUTER_STMT ::= FN
OUTER_STMT ::= OPT_ASSIGN T_SEMICOLON

FN ::= T_LPAREN PL T_RPAREN T_LCURLY INNER_STMTS OPT_RETURN T_RCURLY
PL ::= TSPEC T_IDENTIFIER O_PL
PL ::= Ɛ
O_PL ::= T_COMMA PL
O_PL ::= Ɛ
OPT_RETURN ::= T_RETURN EXPR T_SEMICOLON
OPT_RETURN ::= Ɛ

TSPEC ::= T_INT
TSPEC ::= T_FLOAT
TSPEC ::= T_CHAR
TSPEC ::= T_VOT_IDENTIFIER
TSPEC ::= T_BOOL

INNER_STMTS ::= INNER_STMT INNER_STMTS
INNER_STMTS ::= Ɛ

INNER_STMT ::= DECL_INIT T_SEMICOLON
INNER_STMT ::= T_IDENTIFIER ASSIGN_FNCALL T_SEMICOLON
INNER_STMT ::= IF_STMT
INNER_STMT ::= LOOP
INNER_STMT ::= T_PRINT T_LPAREN ARGS T_RPAREN T_SEMICOLON

DECL_INIT ::= TSPEC T_IDENTIFIER OPT_ASSIGN
OPT_ASSIGN ::= ASSIGN_OP EXPR
OPT_ASSIGN ::= Ɛ

ASSIGN_FNCALL ::= T_LPAREN ARGS T_RPAREN
ASSIGN_FNCALL ::= ASSIGN'
ASSIGN' ::= ASSIGN_OP EXPR
ASSIGN' ::= T_UNARY_PLUS
ASSIGN' ::= T_UNARY_MINUS
ASSIGN_OP ::= T_ASSIGN
ASSIGN_OP ::= T_PLUS_ASSIGN
ASSIGN_OP ::= T_MINUS_ASSIGN
ASSIGN_OP ::= T_STAR_ASSIGN
ASSIGN_OP ::= T_SLASH_ASSIGN

IF_STMT ::= T_IF T_LPAREN EXPR T_RPAREN T_LCURLY INNER_STMTS T_RCURLY ELSE
ELSE ::= T_ELSE O_ELSE
ELSE ::= Ɛ
O_ELSE ::= T_IF T_LPAREN EXPR T_RPAREN T_LCURLY INNER_STMTS T_RCURLY
O_ELSE ::= T_LCURLY INNER_STMTS T_RCURLY

LOOP ::= while T_LPAREN EXPR T_RPAREN T_LCURLY LOOP_STMTS T_RCURLY
LOOP ::= for T_LPAREN OPT_EXPR T_SEMICOLON OPT_EXPR T_SEMICOLON OPT_EXPR T_RPAREN T_LCURLY LOOP_STMTS T_RCURLY
LOOP_STMTS ::= INNER_STMT LOOP_STMTS
LOOP_STMTS ::= T_BREAK T_SEMICOLON LOOP_STMTS
LOOP_STMTS ::= T_CONTINUE T_SEMICOLON LOOP_STMTS
LOOP_STMTS ::= Ɛ

ARGS ::= EXPR OPT_ARGS
ARGS ::= Ɛ
OPT_ARGS ::= T_COMMA ARGS
OPT_ARGS ::= Ɛ

EXPR ::= ADD EXPR'
EXPR ::= T_NEGATION EXPR
EXPR' ::= O_OP EXPR
EXPR' ::= Ɛ
OPT_EXPR ::= EXPR
OPT_EXPR ::= Ɛ

ADD ::= MULT ADD'
ADD' ::= ADD_OP MULT ADD'
ADD' ::= Ɛ

MULT ::= TERM MULT'
MULT' ::= MULT_OP TERM MULT'
MULT' ::= Ɛ

ADD_OP ::= T_PLUS
ADD_OP ::= T_MINUS
MULT_OP ::= T_STAR
MULT_OP ::= T_SLASH
O_OP ::= T_LT
O_OP ::= T_LEQ
O_OP ::= T_GT
O_OP ::= T_GEQ
O_OP ::= T_EQ
O_OP ::= T_NEQ
O_OP ::= T_AND
O_OP ::= T_OR
O_OP ::= T_BITW_AND
O_OP ::= T_BITW_OR

TERM ::= T_LPAREN EXPR T_RPAREN
TERM ::= CONST
TERM ::= T_IDENTIFIER

CONST ::= T_INT_LIT
CONST ::= T_FLOAT_LIT
CONST ::= T_STR_LIT
CONST ::= T_CHAR_LIT
CONST ::= T_BOOL_LIT