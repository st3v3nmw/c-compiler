FNS -> FN FNS
FNS -> ''
FN -> TSPEC id ( PL ) { STMTS return EXPR ; }
PL -> TSPEC id O_PL
PL -> ''
O_PL -> , PL
O_PL -> ''

TSPEC -> int
TSPEC -> float
TSPEC -> char
TSPEC -> void
OPT_TSPEC -> TSPEC
OPT_TSPEC -> ''

FACT -> const
FACT -> id

ADD_OP -> +
ADD_OP -> -
MULT_OP -> *
MULT_OP -> /
O_OP -> <
O_OP -> leq
O_OP -> >
O_OP -> geq
O_OP -> !=
O_OP -> =
O_OP -> and
O_OP -> or
O_OP -> &
O_OP -> |

EXPR -> ADD EXPR'
EXPR -> ! EXPR
EXPR' -> O_OP EXPR
EXPR' -> ''
OPT_EXPR -> EXPR
OPT_EXPR -> ''

ADD -> MULT ADD'
ADD' -> ADD_OP MULT ADD'
ADD' -> ''

MULT -> FACT MULT'
MULT' -> MULT_OP FACT MULT'
MULT' -> ''

STMTS -> STMT STMTS
STMTS -> ''

STMT -> IF_STMT
STMT -> LOOP
STMT -> DECL_ASSIGN

DECL_ASSIGN -> OPT_TSPEC id ASSIGN
ASSIGN -> = EXPR ;
ASSIGN -> ;

IF_STMT -> if ( EXPR ) { STMTS } ELSE
ELSE -> else O_ELSE
ELSE -> ''
O_ELSE -> if ( EXPR ) { STMTS }
O_ELSE -> { STMTS }

LOOP -> while ( EXPR ) { LOOP_STMTS }
LOOP -> for ( OPT_EXPR ; OPT_EXPR ; OPT_EXPR ) { LOOP_STMTS }
LOOP_STMTS -> STMT LOOP_STMTS
LOOP_STMTS -> break ; LOOP_STMTS
LOOP_STMTS -> continue ; LOOP_STMTS
LOOP_STMTS -> ''