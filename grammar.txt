FNS -> FN FNS
FNS -> ''
FN -> TSPEC id ( PL ) { STMTS return EXPR ; }
PL -> TSPEC id O_PL
PL -> ''
O_PL -> , PL
O_PL -> ''

TSPEC -> int
TSPEC -> float
TSPEC -> char
TSPEC -> void

STMTS -> STMT STMTS
STMTS -> ''

STMT -> DECL_INIT
STMT -> ASSIGN
STMT -> IF_STMT
STMT -> LOOP

DECL_INIT -> TSPEC id OPT_ASSIGN ;
OPT_ASSIGN -> = EXPR
OPT_ASSIGN -> ''

ASSIGN -> id ASSIGN' ;
ASSIGN' -> ASSIGN_OP EXPR
ASSIGN' -> ++
ASSIGN' -> --
ASSIGN_OP -> =
ASSIGN_OP -> +=
ASSIGN_OP -> -=
ASSIGN_OP -> *=
ASSIGN_OP -> /=

IF_STMT -> if ( EXPR ) { STMTS } ELSE
ELSE -> else O_ELSE
ELSE -> ''
O_ELSE -> if ( EXPR ) { STMTS }
O_ELSE -> { STMTS }

LOOP -> while ( EXPR ) { LOOP_STMTS }
LOOP -> for ( OPT_EXPR ; OPT_EXPR ; OPT_EXPR ) { LOOP_STMTS }
LOOP_STMTS -> STMT LOOP_STMTS
LOOP_STMTS -> break ; LOOP_STMTS
LOOP_STMTS -> continue ; LOOP_STMTS
LOOP_STMTS -> ''

EXPR -> ADD EXPR'
EXPR -> ! EXPR
EXPR' -> O_OP EXPR
EXPR' -> ''
OPT_EXPR -> EXPR
OPT_EXPR -> ''

ADD -> MULT ADD'
ADD' -> ADD_OP MULT ADD'
ADD' -> ''

MULT -> TERM MULT'
MULT' -> MULT_OP TERM MULT'
MULT' -> ''

ADD_OP -> +
ADD_OP -> -
MULT_OP -> *
MULT_OP -> /
O_OP -> <
O_OP -> <=
O_OP -> >
O_OP -> >=
O_OP -> !=
O_OP -> =
O_OP -> &&
O_OP -> ||
O_OP -> &
O_OP -> |

TERM -> const
TERM -> id